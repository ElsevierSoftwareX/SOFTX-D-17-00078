TITLE:: BusListenerPlotHandler
summary:: A plot handler for the BusListener class.
categories:: Tools>BusListener>Handlers
related:: Classes/BusListener

DESCRIPTION::
This class will plot all selected buses once link::Classes/BusListener#-stop:: is called.

EXAMPLES::

code::
(
s.waitForBoot{
	var bl = BusListener();
	var cbus = Bus.control(Server.default, 3);
	var abus = Bus.audio(Server.default, 4);
	var gate = Bus.audio(Server.default);
	var tool = BusListener.newBusAssignmentTool;

	{
		Out.ar(abus, [SinOsc.ar(40), SinOsc.ar(30), SinOsc.ar(50), SinOsc.ar(10)]);
		Out.kr(cbus, [Saw.kr(2), Saw.kr(4), Saw.kr(10)]);

		// Grab values ~700 times per second
		Out.ar(gate, [Trig1.ar(SinOsc.ar(700), 0)]);
	}.play;

	tool.assign(\Saw2, [cbus.subBus(1)]);
	tool.assign(\AllSaws, [cbus]);
	tool.assign(\AllSins, [abus]);
	tool.assign(\Sin1Saw1, [abus.subBus(0), cbus.subBus(0)]);

	bl
	.addHandler(\PlotSaw2, \plot, tool.indices(\Saw2), "Saw #2")
	.addHandler(\PlotAllSaws, \plot, tool.indices(\AllSaws), "All Saws")
	.addHandler(\PlotAllSins, \plot, tool.indices(\AllSins), "All Sins")
	.addHandler(\PlotSin1Saw1, \plot, tool.indices(\Sin1Saw1), "Sin #1 and Saw #1");

	fork {
		bl.prepare(\global, Server.default, tool.buses, gate, TempoClock.default);
		Server.default.sync;
		bl.start(Server.default, Server.default.defaultGroup);

		5.yield;

		bl.stop;
		// Should spawn 4 plots with:
		// 1) saw # 2
		// 2) all saws
		// 3) all sins
		// 4) saw + sin # 1
	};
};
)
::